# Note that any command in this file might be run by Claude Code
# so they should all be safe. Unsafe commands will go in the justfile
# in the git repo root. In particular deployment commands are put in the root.
#
# Note if the command starts with `@` then it will NOT be echoed to the terminal

set dotenv-load

# Test the code with pytest (HTML report)
default: test-nobdd

# Test the code with pytest (HTML report)
test-html:
	@echo "ðŸš€ Testing code: Running pytest"
	uv run python -m pytest --tb=short --cov --cov-config=pyproject.toml --cov-report=html

# Test the code with pytest (XML report)
test-xml:
	@echo "ðŸš€ Testing code: Running pytest"
	uv run python -m pytest --tb=short --cov --cov-config=pyproject.toml --cov-report=xml

# Test the code with pytest (HTML report)
test: test-html

test-nobdd:
	@echo "ðŸš€ Testing code: Running pytest"
	uv run python -m pytest --tb=short --ignore=tests/bdd --cov --cov-config=pyproject.toml --cov-report=html

# Run BDD tests with pytest-bdd and Playwright
test-bdd:
	@echo "ðŸš€ Running BDD tests"
	uv run python -m pytest tests/bdd/ --verbose --tb=short

# Run BDD tests in headless mode (for CI)
test-bdd-headless:
	@echo "ðŸš€ Running BDD tests (headless)"
	@CI=true uv run python -m pytest tests/bdd/ --verbose --tracing=retain-on-failure

# run the tests when an files change
watch-tests:
	ls *.py | entr uv run pytest --tb=short

# Install the virtual environment, dependencies, an editable copy of this install and install the prek hooks
install:
	@echo "ðŸš€ Creating virtual environment using uv"
	@uv sync
	@echo "ðŸš€ Setting up prek (pre-commit) hooks"
	@uv tool run prek install
	@echo "ðŸš€ Install node dependencies & build sass into css"
	@npm install
	@npm run build:sass
	@echo "ðŸš€ Compile translations"
	@uv run pybabel compile -d translations
	@echo "sym link from AGENTS.md to CLAUDE.md"
	@ln -s AGENTS.md CLAUDE.md

install-dev: install
	@echo "ðŸš€ Installing development dependencies"
	@uv run playwright install

# Run code quality tools.
check:
	@echo "ðŸš€ Checking lock file consistency with 'pyproject.toml'"
	@uv lock --locked
	@echo "ðŸš€ Linting code: Running prek (pre-commit)"
	@uv tool run prek run --all-files
	@echo "ðŸš€ Static type checking: Running mypy"
	@uv run mypy
	@echo "ðŸš€ Checking for obsolete dependencies: Running deptry"
	@uv run deptry src

# Run code quality tools - CI edition
check-ci:
	@echo "ðŸš€ Checking lock file consistency with 'pyproject.toml'"
	@uv lock --locked
	@echo "ðŸš€ Linting code: Running prek (pre-commit)"
	@uv tool run prek run --all-files --config ../.pre-commit-config-ci.yaml
	@echo "ðŸš€ Static type checking: Running mypy"
	@uv run mypy
	@echo "ðŸš€ Checking for obsolete dependencies: Running deptry"
	@uv run deptry src

# start the flask shell
flask-shell:
  @uv run flask shell

# start the flask shell
flsh: flask-shell

# run flask locally
flask $FLASK_APP="src/opendlp/entrypoints/flask_app.py":
  @uv run flask run --debug

# run flask for BDD tests - they will use this server if it exists
flask-bdd $FLASK_APP="src/opendlp/entrypoints/flask_app.py" $DB_PORT="54322" $FLASK_ENV="testing_postgres":
  @uv run flask run --port=5002 --host=127.0.0.1 --debug

# run a celery worker (for local dev)
celery:
  @uv run celery --app opendlp.entrypoints.celery.tasks worker --loglevel=info

# run gunicorn locally
gunicorn:
  @uv run gunicorn --bind=0.0.0.0:8081 --logger-class=opendlp.logging.GunicornLogger --log-level=debug 'opendlp.entrypoints.flask_app:create_app()'

# build CSS from SCSS
build-css:
  @echo "ðŸš€ Building CSS from SCSS"
  @npm run build:sass

# watch and rebuild CSS from SCSS
watch-css:
  @echo "ðŸš€ Watching and building CSS from SCSS"
  @npm run watch:sass

# run flask locally
run: build-css flask

# connect to docker postgres (local services)
psql:
  @echo "password is abc123"
  psql --host 127.0.0.1 --port 54321 --user opendlp opendlp

# connect to docker postgres (tests)
psql-test:
  @echo "password is abc123"
  psql --host 127.0.0.1 --port 54322 --user opendlp opendlp

# run the database migrations
migrate:
  uv run alembic upgrade head

# extract strings to translate from code and templates
translate-regen:
  @echo "ðŸš€ Extracting translatable strings from app and sortition-algorithms library"
  uv run pybabel extract \
    --mapping-file=babel.cfg \
    --keyword=_l -keyword=N_ \
    --output-file=translations/messages.pot \
    --ignore-dirs=thirdparty \
    . `uv run python -c "import sortition_algorithms, pathlib; print(pathlib.Path(sortition_algorithms.__file__).parent.relative_to(pathlib.Path.cwd()))"`
  uv run pybabel update --input-file=translations/messages.pot --output-dir=translations

# compile translations from .po files so flask can use them
translate-compile:
  uv run pybabel compile --directory=translations

# shut down docker
stop-docker:
	@docker compose down

# rebuild docker image
build-docker:
	@git show --no-patch --format='%cd %h' --date=format:'%Y-%m-%d' HEAD > generated_version.txt
	@docker compose build

# start docker and detach - note NO WATCH
start-docker:
	@docker compose up --detach

# start docker watching for changes and blocking - i.e. not detaching
start-docker-b:
	@docker compose up --watch

# stop, rebuild, start docker and detach
restart-docker: stop-docker build-docker start-docker

# stop, rebuild, start docker blocking - i.e. not detaching
restart-docker-b: stop-docker build-docker start-docker-b

# start the postgres only docker composer for services for local dev
start-local-docker:
  @docker compose -f compose.localdev.yaml up --detach

# stop the postgres only docker composer for services for local dev
stop-local-docker:
  @docker compose -f compose.localdev.yaml down

# start the postgres only docker composer for services for tests
start-test-docker:
  @docker compose -f compose.test.yaml up --detach

# stop the postgres only docker composer for services for tests
stop-test-docker:
  @docker compose -f compose.test.yaml down

# drop the docker volume holding the postgres database for local services
drop-local-docker-db:
  @docker volume rm backend_sqla_opendlp_local_pgdata

# drop the docker volume holding the postgres database for test
drop-test-docker-db:
  @docker volume rm backend_sqla_opendlp_test_pgdata

# run the editor with the environment available
edit:
  @uv run $EDITOR

# run the editor with the environment available
e: edit
# run the editor with the environment available
vim: edit
